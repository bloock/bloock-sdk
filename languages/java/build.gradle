import org.jetbrains.dokka.gradle.DokkaTask

buildscript {
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.20"
    }
}

plugins {
    id 'java-library'
    id 'com.google.protobuf' version '0.9.1'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'org.jetbrains.dokka' version '1.7.20'
}
apply plugin: 'kotlin'

group = 'com.bloock.sdk'
version = '2.9.7'

repositories {
    maven { url "https://maven-central.storage-download.googleapis.com/maven2/" }
    mavenCentral()
}

def grpcVersion = '1.50.2'
def protobufVersion = '3.21.9'
def protocVersion = protobufVersion

dependencies {
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    // Protobuf + GRPC
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "commons-codec:commons-codec:1.15"
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"

    // FFI
    implementation 'net.java.dev.jna:jna:5.12.1'

    // Testing
    testImplementation "junit:junit:4.12"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'

    // Documentation
    dokkaHtmlPlugin('org.jetbrains.dokka:kotlin-as-java-plugin:1.7.20')
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { task ->
            grpc {}
        }
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

sourceSets {
    main {
        proto {
            srcDir '../../bloock-bridge/proto'
        }
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }

    compat {
        java {
            srcDirs "$projectDir/src/compat/java"
        }

        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    compatImplementation.extendsFrom testImplementation
    compatRuntime.extendsFrom testRuntime
}

task compatTest(type: Test) {
    testClassesDirs = sourceSets.compat.output.classesDirs
    classpath = sourceSets.compat.runtimeClasspath
    outputs.upToDateWhen { false }
}
check.dependsOn compatTest

tasks.withType(Jar).all {
    duplicatesStrategy 'exclude'
}

java {
    withJavadocJar()
    withSourcesJar()
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'Bloock SDK'
                description = 'Bloock SDK for Java'
                url = 'https://www.bloock.com'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/bloock/bloock-sdk/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'bloock_it'
                        name = 'BLOOCK'
                        email = 'it@bloock.com'
                    }
                }
                scm {
                    url = 'https://www.bloock.com'
                    connection = 'scm:git:https://github.com/bloock/bloock-sdk.git'
                    developerConnection = 'scm:git:https://github.com/bloock/bloock-sdk.git'
                }
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

tasks.withType(DokkaTask.class) {
    outputDirectory.set(file("docs"))

    dokkaSourceSets.configureEach {
        perPackageOption {
            matchingRegex.set("com.bloock.sdk.bridge*")
            suppress.set(true)
        }
        perPackageOption {
            matchingRegex.set("com.bloock.sdk.config*")
            suppress.set(true)
        }
        perPackageOption {
            matchingRegex.set("com.bloock.sdk.ffi*")
            suppress.set(true)
        }
    }
}
