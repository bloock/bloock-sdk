plugins {
    // Provide convenience executables for trying out the examples.
    id 'application'
    // ASSUMES GRADLE 5.6 OR HIGHER. Use plugin version 0.8.10 with earlier gradle versions
    id 'com.google.protobuf' version '0.8.18'
    // Generate IntelliJ IDEA's .idea & .iml project files
    id 'idea'
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/maven2/" }
    mavenCentral()
    mavenLocal()
}

version = '0.1.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def grpcVersion = '1.48.0'
def protobufVersion = '3.21.1'
def protocVersion = protobufVersion

dependencies {
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    // Protobuf + GRPC
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"

    // FFI
    implementation 'net.java.dev.jna:jna:5.12.1'

    // Testing
    testImplementation "junit:junit:4.12"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation "org.mockito:mockito-core:3.4.0"
}

application {
    // Define the main class for the application.
    mainClass = 'bloock.sdk.java.App'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        proto {
            srcDir '../../../bloock-bridge/proto'
        }
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}
